let FromTime = datetime(2024-01-30T06:30:00); //change the date and time in YYYY-MM-DDTHH:MM:SS
let TillTime = datetime(2024-01-30T18:30:00); //chage the date and time (ie.12 hours later)
let GeoIPDB_FULL = _GetWatchlist("IPtoGeoList");
let IpAddress_REGEX_PATTERN = @"\b(?:[0-9]{1,3}\.){3}[0-9]{1,3}\b";
Syslog
| where Facility == "auth"
| where SyslogMessage startswith "Failed password"
| where TimeGenerated >= FromTime and TimeGenerated <= TillTime
| project TimeGenerated, SourceIP = extract(IpAddress_REGEX_PATTERN, 0, SyslogMessage), DestinationHostName = HostName, DestinationIP = HostIP, Facility, SyslogMessage, ProcessName, SeverityLevel, Type
| evaluate ipv4_lookup(GeoIPDB_FULL, SourceIP, network)
| project TimeGenerated, SourceIP, DestinationHostName, DestinationIP, Facility, SyslogMessage, ProcessName, SeverityLevel, Type, latitude, longitude, city = cityname, country = countryname, friendly_location = strcat(cityname, " (", countryname, ")");
